#
# My best attempt to emulate Oh-My-Zsh's "Crush" theme
#
# Authors:
#   Steven Barnett <steven.abarnett@gmail.com>
#
# Features:
#   - Timestamp
#   - Working directory (using Sorin's as a template)
#   - VCS branch with dirty / clean indicator
#   - Last command exit status is displayed when non-zero
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_stevendesu_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd

  # Run vcs_info
  vcs_info

  # Determine git clean / dirty status
  if git rev-parse --git-dir > /dev/null 2>&1; then
    if [[ $(git status --porcelain 2> /dev/null | wc -l) == 0 ]]; then
      _prompt_stevendesu_git_dirty=" %F{2}#%f"
    else
      _prompt_stevendesu_git_dirty=" %F{1}#%f"
    fi
  else
    _prompt_stevendesu_git_dirty=""
  fi
}

function prompt_stevendesu_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_stevendesu_precmd

  # Enable VCS systems
  zstyle ':vcs_info:*' enable bzr git hg svn

  # Style VCS prompt
  zstyle ':vcs_info:*' actionformats ':%F{2}%b%f (%F{3}%a%f)'
  zstyle ':vcs_info:*' formats ':%F{2}%b%f'

  # Define prompts.
  PROMPT='%(?..%F{1}%B-> [%?]%b%f
)%F{5}{%F{3}%T%F{5}}%f|%F{6}${_prompt_sorin_pwd}%f${vcs_info_msg_0_}${_prompt_stevendesu_git_dirty} >'
  RPROMPT=''
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

prompt_stevendesu_setup "$@"
